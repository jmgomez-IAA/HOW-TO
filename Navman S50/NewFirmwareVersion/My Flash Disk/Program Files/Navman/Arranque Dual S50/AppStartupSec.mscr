###############################################################################
#                                                                             #
#  Navman Unlock   ver. 3                                                     #
#                                                                             #
###############################################################################

DeviceFile = SystemPath("ScriptPath")\"Device.ini"
ResetFile  = SystemPath("ScriptPath")\"Reset.dat"
Nanman_org = SystemPath("ScriptPath")\"AppStartupSec_Org.exe"


#si se acaba de encender
if (RegRead("HKLM", "Comm", "BootCount") = 1)

	SetChoiceEntryFormat(28, 20, "Verdana")
	Choice("Quick Launch Selection", "*** Menu Inicio***","Modo Normal","Navman Original", "Otra Aplicación")
	
	case 2
		RegWriteString("HKLM", "SOFTWARE\Unlock", "Mode","2")
 		Run(Nanman_org)
		exit
	case 3
		RegWriteString("HKLM", "SOFTWARE\Unlock", "Mode","3")
		sFile = SelectFile( "Seleccione Aplicacion",FALsE)
		if(length(sFile))
			RegWriteString("HKLM", "SOFTWARE\Unlock", "Run",sFile)
			run(sFile)
			exit
		endif
	EndChoice

	RegWriteString("HKLM", "SOFTWARE\Unlock", "Mode","1")

	# Comprobacion del tiempo entre  reinicios Hardware ciclicos
	if (FileExists(ResetFile))
		if (TimeStamp() - 120 < ReadFile(ResetFile))
			Message("Proteccion reinicios Hardware ","Proteccion")
		endif
	else
		Message("No se encontrarón los datos del reinicio anterior","Proteccion")
	endif

	WriteFile(ResetFile,TimeStamp())

	# Comprobacion de reinicios ciclicos
	if (RegValueExists("HKLM", "SOFTWARE\Unlock", "InstallDir"))
 		 Message("Error en script","Proteccion")
 	else
   		 RegWriteString("HKLM", "SOFTWARE\Unlock", "InstallDir",SystemPath("ScriptPath"))
		 RegWriteString("HKLM", "SOFTWARE\Unlock", "Ver","2")
	endif

	# Comprobacion de reinicios Hardware ciclicos
	if (IniRead(DeviceFile, "Reset","Reset") eq "Hard")
		Message("Reset Hardware consecutivos","Proteccion")
	else
		IniWrite(DeviceFile, "Reset","Reset","Hard")  
	endif

	#Amplia la memoria
  	runwait(IniRead(DeviceFile, "Files", "SetMemory"))


	#Cambia clave de inicio
	RegDelete("HKLM", "init", "Launch80")
	RegWriteString("HKLM" , "init" , "Launch99" , "\My Flash Disk\Program Files\Navman\AppStartupSec.exe")

	# Register "Explorer"
	if (RegValueExists("HKLM", "Explorer\Desktop","{000214A0-0000-0000-C000-000000000046}"))	
		RegDelete("HKLM","Explorer\Desktop","{000214A0-0000-0000-C000-000000000046}")
	endif
	if (RegValueExists("HKLM","Explorer\Desktop","{000214A1-0000-0000-C000-000000000046}"))
		RegDelete("HKLM","Explorer\Desktop","{000214A1-0000-0000-C000-000000000046}")
	endif

	# Register "TascalSoft RegEdit"
	TreProgram = IniRead(DeviceFile, "Files", "Tree")
	RegWriteString("HKCR", ".reg", "", "regfile")
	RegWriteString("HKCR", "regfile", "", "Registry File")
 	RegWriteString("HKCR", "regfile\DefaultIcon", "", TreProgram & ",-101")
	RegWriteString("HKCR", "regfile\Shell\Open\Command", "","""" & TreProgram & """ ""%1""")

	# Configura MortScript
	MortScript = IniRead(DeviceFile, "Files", "MortScript")
	RegWriteString("HKCR", ".mortrun", "", "MortScript")
 	RegWriteString("HKCR", ".mscr", "", "MortScript")
 	RegWriteString("HKCR", "MortScript", "", "MortScript")
	RegWriteString("HKCR", "MortScript\DefaultIcon", "",MortScript & ",-130")
 	RegWriteString("HKCR", "MortScript\Shell\Open\Command", "","""" & MortScript & """ ""%1""")

	#Exporta los ficheros de registro
	ForEach regFile in files("\My Flash Disk\System\RegFiles\*.reg")
		RunWait(TreProgram, "-s " & """" & regFile & """")
	EndForEach
	
	#Agrega dll como directorio de sistema
	RegWriteMultiString("HKLM", "Loader", "SystemPath", Array("\My Flash Disk\System\Dll\","\My Flash Disk\System\Shell\"))

	#copia los ficheros al directorio windows
	xcopy("\My Flash Disk\System\windows\*.*","\windows",TRUE,TRUE)

	#script autorung
	if (FileExists("\My Flash Disk\System\Autorun.mscr"))
		callscript("\My Flash Disk\System\Autorun.mscr")
	endif

	#Reinicio
	softreset =  IniRead(DeviceFile, "Files","SoftReset")
	if (FileExists(softreset))
		Run(softreset)
	endif

else
	# Comprobacion de reinicios ciclicos
	if (RegValueExists("HKLM", "SOFTWARE\Unlock", "StartUpTime"))
 		# Comprobacion de reinicios
  		if (TimeStamp() - 120 < RegRead("HKLM", "SOFTWARE\Unlock", "StartUpTime"))
    			Message("Proteccion contra reinicios ciclicos","Proteccion")
  		endif

	endif
   	RegWriteString("HKLM", "SOFTWARE\Unlock", "StartUpTime",TimeStamp())
	IniWrite(DeviceFile, "Reset","Reset","Soft")	

endif

  

###############################################################################
#
#	INICIO
#
###############################################################################




vMode= RegRead("HKLM", "SOFTWARE\Unlock", "Mode")

if (vMode = "2")
	Run(Nanman_org)
	exit
elseif (vMode = "3"
	sFile = RegRead("HKLM", "SOFTWARE\Unlock", "Run")
	run(sFile)
	exit
endif


TaskMan = IniRead(DeviceFile, "Files", "TaskMan")
Appinit = IniRead(DeviceFile, "Files", "AppInit")
AppinitSD   = IniRead(DeviceFile, "Files", "AppInitSD")
SmartSt = IniRead(DeviceFile, "Files", "SmartST")
MscrStartUp = IniRead(DeviceFile, "Files", "MscrStartUp")

count = 0
While(count < 15)
	if (DirExists("\Storage Card"))
		count = 21
	else
		Sleep(100)
		count = count + 1
	endif
EndWhile

if (FileExists(TaskMan))
	Run(TaskMan)
endif

if (FileExists(AppinitSD))
	Run(AppinitSD)

elseif (FileExists(Appinit))
	Run(Appinit)

elseif (FileExists(SmartSt))
	Run(SmartSt)

endif

if (FileExists(MscrStartUp))
	CallScript(MscrStartUp)
endif