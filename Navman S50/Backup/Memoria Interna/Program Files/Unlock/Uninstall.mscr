###############################################################################
#                                                                             #
#  Navman Unlock                                                              #
#                                                                             #
###############################################################################


DeviceFile = SystemPath("ScriptPath") & "\Device.ini"


if (not ProcExists("Control.exe"))
  if (Question("Deseas desinstalar el UNLOCK?", "Unload", \
    "YesNoCancel") <> Yes)
    Exit
  endif
endif


# Uninstall File Unlock hack if needed
if (FileExists(IniRead(DeviceFile, "Files", "HackFile")) \
  and FileExists(IniRead(DeviceFile, "Files", "HackBackup")))

  HackFileDir = IniRead(DeviceFile, "Files", "HackFile")
  HackFileName = SubStr(HackFileDir, \
    ReverseFind(HackFileDir, "\") + 1)
  HackFileName = SubStr(HackFileName, 1, \
    ReverseFind(HackFileName, ".") - 1)
  HackFileDir = SubStr(HackFileDir, 1, \
    ReverseFind(HackFileDir, "\") - 1)

  Delete(HackFileDir & "\" & HackFileName & ".exe")
  Delete(HackFileDir & "\" & HackFileName & ".mscr")
  Delete(HackFileDir & "\Device.ini")
  Delete(HackFileDir & "\MortScript.exe")

  Rename(IniRead(DeviceFile, "Files", "HackBackup"), \
    IniRead(DeviceFile, "Files", "HackFile"))
endif


# Uninstall created files / folders
DelTree(IniRead(DeviceFile, "Dirs", "Install") & "\Programs")
DelTree(IniRead(DeviceFile, "Dirs", "Install") & "\StartUp")
DelTree(IniRead(DeviceFile, "Dirs", "Install") & "\Desktop")


# Restore original registry setting
if (RegValueExists("HKCU", "ControlPanel\Desktop", "Wallpaper"))
  RegWriteString("HKCU", "ControlPanel\Desktop", "Wallpaper", \
    RegRead("HKLM", "SOFTWARE\Unlock\Backup", "Wallpaper"))
endif


# Uninstall created registry informations
RegDeleteKey("HKLM", "Software\Unlock", True, True)


# Terminate all programs to allow the removing
Kill("TRE.exe")
Kill("PNADesktop.exe")
foreach Dir in Directories (IniRead(DeviceFile, "Dirs", "Install") & "\*.*")
  foreach Program in Files (Dir & "\*.exe")
    Kill(SubStr(Program, ReverseFind(Program, "\") + 1))
  endforeach
endforeach


if (ProcExists("Control.exe"))
  Run(IniRead(DeviceFile, "Files", "HackFile"))
else
  InstallName = IniRead(DeviceFile, "Device", "Name") & " Unlock"
  DelTree("\Windows\" & InstallName & ".unload")
  DelTree("\Windows\AppMgr\" & InstallName & ".*")
  RegDeleteKey("HKLM", "Software\Apps\" & InstallName, True, True)

  Message("Navman Unlock desinstalado.^NL^^NL^" \
    & "Borra la carpeta """ & IniRead(DeviceFile, "Dirs", "Install") \
    & """ manualmente desde el PC.^NL^^NL^" \
    & "Presiona Ok para reiniciar.","Desinstalacion")
  Reset
endif
